group 'SpringBootReactExample'
version '1.0-SNAPSHOT'

buildscript {
    ext {
        springBootVersion = '2.0.0.M4'
    }
    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "http://repo.spring.io/release" }
        maven { url "http://repo.spring.io/milestone" }
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
    }

}
apply plugin: "com.moowork.node"
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
    maven { url "http://repo.spring.io/release" }
    maven { url "http://repo.spring.io/milestone" }
    maven { url "http://repo.spring.io/snapshot" }

}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-data-rest:${springBootVersion}")

    testCompile group: 'junit', name: 'junit', version: '4.12'
}


node {
    version = '9.3.0'
    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true

    // Set the work directory where node_modules should be located
    nodeModulesDir = file("${project.projectDir}/frontend")
}

task webpack(type: YarnTask, dependsOn: yarn) {
    inputs.files(fileTree("${project.projectDir}/frontend/"))
    outputs.dir("${project.projectDir}/src/main/resources/static/")

    args = ['build']
}

task moveJStoSpring(type: Copy) {
    from "${project.projectDir}/frontend/build/"
    into "${project.projectDir}/src/main/resources/static/"
}

moveJStoSpring.dependsOn 'webpack'
processResources.dependsOn 'moveJStoSpring'